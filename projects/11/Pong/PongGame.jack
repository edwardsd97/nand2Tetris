// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/11/Pong/PongGame.jack

/**
 * Represents a Pong game.
 */
class PongGame {

    static PongGame instance; // the singelton, a Pong game instance     
    field Bat bat;            // the bat
    field Ball ball;          // the ball
    field int wall;           // the current wall that the ball is bouncing off of.
    field boolean exit;       // true when the game is over
    field int score;          // the current score.
    field int lastWall;       // the last wall that the ball bounced off of.

    // The current width of the bat
    field int batWidth;

    /** Constructs a new Pong game. */
    constructor PongGame new() {
        batWidth = 50;  // initial bat size
        bat = Bat.new(230, 229, batWidth, 7);
        ball = Ball.new(253, 222, 0, 511, 0, 229);
        ball.setDestination(400,0);
        Screen.drawRectangle(0, 238, 511, 240);
	    Output.moveCursor(22,0);
	    Output.printString("Score: 0");
	    exit = false;
	    score = 0;
	    wall = 0;
	    lastWall = 0;
        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        bat.dispose();
	    ball.dispose();
        Memory.deAlloc(this);
        return;
    }

    /** Creates an instance of Pong game, and stores it. */
    function void newInstance() {
        instance = PongGame.new();
        return;
    }
    
    /** Returns the single instance of this Pong game. */
    function PongGame getInstance() {
        return instance;
    }

    /** Starts the game, and andles inputs from the user that control
     *  the bat's movement direction. */
    method void run() {
        var char key;

        while (~exit) {
            // waits for a key to be pressed.
            while ((key = 0) & (~exit)) {
                key = Keyboard.keyPressed();
                bat.move();
                moveBall();
                Sys.wait(50);
            }

            if (key = 130) { bat.setDirection(1); }
	        else {
	            if (key = 132) { bat.setDirection(2); }
		        else {
                    if (key = 140) { exit = true; }
		        }
            }

            // Waits for the key to be released.
            while ((~(key = 0)) & (~exit)) {
                key = Keyboard.keyPressed();
                bat.move();
                moveBall();
                Sys.wait(50);
            }
        }

	    if (exit) {
            Output.moveCursor(10,27);
	        Output.printString("Game Over");
	    }
            
        return;
    }

    /**
     * Handles ball movement, including bouncing.
     * If the ball bounces off a wall, finds its new direction.
     * If the ball bounces off the bat, increases the score by one
     * and shrinks the bat's size, to make the game more challenging. 
     */
    method void moveBall() {
        var int bouncingDirection, batLeft, batRight, ballLeft, ballRight;

        wall = ball.move();

        if ((wall > 0) & (~(wall = lastWall))) {
             lastWall = wall;
             bouncingDirection = 0;
             batLeft = bat.getLeft();
             batRight = bat.getRight();
             ballLeft = ball.getLeft();
             ballRight = ball.getRight();
  
            if (wall = 4) {
                 exit = (batLeft > ballRight) | (batRight < ballLeft);
                if (~exit) {
                    if (ballRight < (batLeft + 10)) {  bouncingDirection = -1; }
                    else {
                        if (ballLeft > (batRight - 10)) {  bouncingDirection = 1; }
                    }

                     batWidth = batWidth - 2;
                     bat.setWidth(batWidth);      
                     score = score + 1;
                     Output.moveCursor(22,7);
                     Output.printInt(score);
                }
            }
             ball.bounce(bouncingDirection);
        }
        return;
    }
}