
class Sprite
{
    field int x, y; // screen coordinate

    // FIXME : A SpriteLib.Enum would be nice
    field int type; // 0 - car 
                    // 1 - line
                    // 2 - oil slick

    field int velX; // X Velocity
    field int velY; // Y Velocity

    field int visible; // Visible or not

    field int centerX; // abs distance from center x the sprite is allowed to be
    field int centerY; // abs distance from center y the sprite is allowed to be

    constructor Sprite new( int typeIn )
    {
        let type = typeIn;
        let x = 0;
        let y = 0;
        let visible = -1;

        // -1 used to denote undefined here
        let centerX = -1;
        let centerY = -1;

        return this;
    }

    method void clamp()
    {
        do clampX();
        do clampY();
        return;
    }

    method void clampX()
    {
        var int left;
        var int right;

        let left = 0;
        let right = 512 - 16;

        if ( ( centerX > -1 ) )
        {
            let left = 256 - centerX;
            let right = ( 256 + centerX ) - 16;
        }

        if ( x < left )
        {
            let x = left;
        }

        if ( x > right )
        {
            let x = right;
        }

        return;
    }

    method void clampY()
    {
        var int top;
        var int bottom;

        let top = 0;
        let bottom = 256 - 16;

        if ( ( centerY > -1 ) )
        {
            let top = 128 - centerY;
            let bottom = ( 128 + centerY ) - 16;
        }

        if ( y < top )
        {
            let y = top;
        }

        if ( y > bottom )
        {
            let y = bottom;
        }

        return;
    }

    method void setType( int typeIn )
    {
        let type = typeIn;
        return;
    }

    method void setCenterX( int xFromCenter )
    {
        let centerX = xFromCenter;
        return;
    }

    method void setCenterY( int yFromCenter )
    {
        let centerY = yFromCenter;
        return;
    }

    method int getX()
    {
        return x;
    }

    method int getY()
    {
        return y;
    }

    method void setX( int xIn )
    {
        let x = xIn;
        do clampX();
        return;
    }

    method void setY( int yIn )
    {
        let y = yIn;
        do clampY();
        return;
    }

    method void setXY( int xIn, int yIn )
    {
        let x = xIn;
        let y = yIn;
        do clamp();
        return;
    }

    method void setVelocity( int xIn, int yIn )
    {
        let velX = xIn;
        let velY = yIn;
        return;
    }

    method void setVelocityX( int xIn )
    {
        let velX = xIn;
        return;
    }

    method int getVelocityX()
    {
        return velX;
    }

    method void setVelocityY( int yIn )
    {
        let velY = yIn;
        return;
    }

    method int getVelocityY()
    {
        return velY;
    }

    method void setVisible( int visibleIn )
    {
        let visible = visibleIn;
        if ( visible )
        {
            let visible = -1;
        }
        return;
    }

    method int getVisible()
    {
        return visible;
    }

    method int touching( Sprite other )
    {
        var int diffX;
        var int diffY;
        var int isVisible;

        let isVisible = getVisible();
        if ( ~isVisible )
        {
            return 0;
        }

        let isVisible = other.getVisible();
        if ( ~isVisible )
        {
            return 0;
        }

        let diffX = x - other.getX();
        if ( diffX < 0 )
        {
            let diffX = diffX * -1;
        }
        if ( diffX > 14 )
        {
            return 0;
        }

        let diffY = y - other.getY();
        if ( diffY < 0 )
        {
            let diffY = diffY * -1;
        }
        if ( diffY > 14 )
        {
            return 0;
        }

        // FIXME - extend this by doing a per pixel comparison
        return -1;
    }

    method void moveX( int xDelta )
    {
        let x = x + xDelta;
        do clampX();
        return;
    }

    method void moveY( int yDelta )
    {
        let y = y + yDelta;
        do clampY();
        return;
    }

    method void update()
    {
        // FIXME - extend this by updating based on time rather than whatever update rate is being maintained
        if ( ~(velX = 0) )
        {
            do moveX( velX );
        }

        if ( ~(velY = 0) )
        {
            do moveY( velY );
        }

        return;
    }

    method void draw()
    {
        if ( ~visible )
        {
            return;        
        }

        if ( ( type = 0 ) ) // car
        {
           do SpriteLib.drawCar( x, y );
        }
        
        if ( ( type = 1 ) ) // line
        {
           do SpriteLib.drawLine( x, y );
        }
        
        if ( ( type = 2 ) ) // oil slick
        {
           do SpriteLib.drawOilSlick( x, y );
        }

        return;
    }

    method void clear()
    {
        if ( ~visible )
        {
            return;        
        }

        do SpriteLib.clear( x, y );

        return;
    }

    method void dispose()
    {
        do Memory.deAlloc( this );
        return;
    }
}
